Filter {
    Expr 
        = FilterExpr
        | PropertiesExpr
        | PropertyIdentifierExpr

    FilterExpr = BoolOr
    
    BoolOr
        = BoolOr orOperation BoolAnd -- or
        | BoolAnd

    BoolAnd 
        = BoolAnd andOperation PriExp  --and
        | PriExp
        
    PriExp 
        = "(" FilterExpr ")" -- paren
        | ComparisonExp

    ComparisonExp = EqualityExpr | LessThanExpr | MoreThanExpr | LessThanOrEqualExpr | MoreThanOrEqualExpr | NonEqualityExpr | StartsWithExpr | EndsWithExpr | exactElement

    EqualityExpr = propertySequence "=" expressionConst
    
    LessThanExpr = propertySequence "<" expressionConst
    
    MoreThanExpr = propertySequence ">" expressionConst
    
    LessThanOrEqualExpr = propertySequence "<=" expressionConst
    
    MoreThanOrEqualExpr = propertySequence ">=" expressionConst

    NonEqualityExpr = propertySequence nonEqualSign expressionConst

    StartsWithExpr = propertySequence likeWord startsWithConst

    EndsWithExpr = propertySequence likeWord endsWithConst

    PropertiesExpr = PropertyIdentifierExpr ("," PropertyIdentifierExpr)*

    PropertyIdentifierExpr
        = NamedPropertyExpr
        | propertySequence

    NamedPropertyExpr = propertySequence asWord propertyNameIdentifier

    propertyNameIdentifier = (letter | digit)+

    propertySequence = property | directProperty
                    
    property 
        =  propertySequence "." categoryOrProperty --ofPropertySequence
        | directAnyPropertySequence "." categoryOrProperty --ofDirectAnyPropertySequence
        | directAnyProperty "." categoryOrProperty --ofDirectAny
                    
    directProperty 
        = categoryOrProperty "." categoryOrProperty --ofCategory
        | anyProperty "." categoryOrProperty --ofAnyProperty
        
    directAnyPropertySequence = propertySequence "." anyProperty
        
    directAnyProperty = directAnyProperty "." anyProperty --sequenced
        | categoryOrProperty "." anyProperty --ofCategory
        | anyProperty "." anyProperty --ofAny
        
    exactElement = propertySequence "!" --ofPropertySequence
        | categoryOrProperty "!" --ofCategory
    
    categoryOrProperty (element category or property)
        = "[" identificator "]" --inBrackets 
        | alnum+ --value

    asWord = ("A" | "a")("S" | "s")

    nonEqualSign = "<>" | "!="
        
    andOperation = andWord | "&&" | "&"

    andWord = ("A" | "a")("N" | "n")("D" | "d")
    
    orOperation = orWord | "||" | "|"

    orWord = ("O" | "o")("R" | "r")

    likeWord = ("L" | "l")("I" | "i")("K" | "k")("E" | "e")
    
    anyProperty = "*"
    
    identificator = (~"]" ~"[" any)+
    
    expressionConst = textConst | number

    startsWithConst
        = "\"" likeTextValue "%\""

    endsWithConst
        = "\"%" likeTextValue "\""
    
    textConst (target text value)
        = "\"" textValue "\""
        
    textValue = textChar*

    likeTextValue = likeTextChar*
    
    textChar
        = "\\\""
        | (~"\"" any)

    likeTextChar
        = "\\\""
        | "\\%"
        | (~("\"" | "%") any)
        
    number
      = positiveNumber
      | negativeNumber
      
    negativeNumber = "-" positiveNumber
    
    positiveNumber = realNumber | integerNumber
      
    integerNumber = digit+
    
    realNumber = digit* "." digit+
}