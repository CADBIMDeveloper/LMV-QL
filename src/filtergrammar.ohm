Filter {
    Exp = BoolOr
    
    BoolOr
        = BoolOr orOperation BoolAnd -- or
        | BoolAnd

    BoolAnd 
        = BoolAnd andOperation PriExp  --and
        | PriExp
        
    PriExp 
        = "(" Exp ")" -- paren
        | ComparisonExp

    ComparisonExp = EqualityExpr | LessThanExpr | MoreThanExpr | LessThanOrEqualExpr | MoreThanOrEqualExpr | NonEqualityExpr | exactElement

    EqualityExpr = propertySequence "=" expressionConst
    
    LessThanExpr = propertySequence "<" expressionConst
    
    MoreThanExpr = propertySequence ">" expressionConst
    
    LessThanOrEqualExpr = propertySequence "<=" expressionConst
    
    MoreThanOrEqualExpr = propertySequence ">=" expressionConst

    NonEqualityExpr = propertySequence nonEqualSign expressionConst

    propertySequence = property | directProperty
                    
    property 
        =  propertySequence "." categoryOrProperty --ofPropertySequence
        | directAnyPropertySequence "." categoryOrProperty --ofDirectAnyPropertySequence
        | directAnyProperty "." categoryOrProperty --ofDirectAny
                    
    directProperty 
        = categoryOrProperty "." categoryOrProperty --ofCategory
        | anyProperty "." categoryOrProperty --ofAnyProperty
        
    directAnyPropertySequence = propertySequence "." anyProperty
        
    directAnyProperty = directAnyProperty "." anyProperty | categoryOrProperty "." anyProperty | anyProperty "." anyProperty
        
    exactElement = propertySequence "!" --ofPropertySequence
        | categoryOrProperty "!" --ofCategory
    
    categoryOrProperty (element category or property)
        = "[" identificator "]" --inBrackets 
        | alnum+ --value

    nonEqualSign = "<>" | "!="
        
    andOperation = andWord | "&&" | "&"

    andWord = ("A" | "a")("N" | "n")("D" | "d")
    
    orOperation = orWord | "||" | "|"

    orWord = ("O" | "o")("R" | "r")
    
    anyProperty = "*"
    
    identificator = (~"]" ~"[" any)+
    
    expressionConst = textConst | number
    
    textConst (target text value)
        = "\"" textValue "\""
        
    textValue = textChar*
    
    textChar
        = "\\\""
        | (~"\"" any)
        
    number
      = positiveNumber
      | negativeNumber
      
    negativeNumber = "-" positiveNumber
    
    positiveNumber = realNumber | integerNumber
      
    integerNumber = digit+
    
    realNumber = digit* "." digit+
}