Filter {
    Exp = BoolOr
    
    BoolOr
        = PriExp orOperation Exp -- or
        | BoolAnd

    BoolAnd 
        = PriExp andOperation Exp  --and
        | PriExp
        
    PriExp 
        = "(" Exp ")" -- paren
        | ComparisonExp

    ComparisonExp = EqualityExpr | LessThanExpr | MoreThanExpr | LessThanOrEqualExpr | MoreThanOrEqualExpr | exactElement

    EqualityExpr = propertySequence "=" expressionConst
    
    LessThanExpr = propertySequence "<" expressionConst
    
    MoreThanExpr = propertySequence ">" expressionConst
    
    LessThanOrEqualExpr = propertySequence "<=" expressionConst
    
    MoreThanOrEqualExpr = propertySequence ">=" expressionConst

    propertySequence = property | directProperty
                    
    property 
        =  propertySequence "." categoryOrProperty
        | directAnyPropertySequence "." categoryOrProperty
        | directAnyProperty "." categoryOrProperty
                    
    directProperty 
        = categoryOrProperty "." categoryOrProperty 
        | anyProperty "." categoryOrProperty
        
    directAnyPropertySequence = propertySequence "." anyProperty
        
    directAnyProperty = directAnyProperty "." anyProperty | categoryOrProperty "." anyProperty | anyProperty "." anyProperty
        
    exactElement = propertySequence "!" --ofPropertySequence
        | categoryOrProperty "!" --ofCategory
    
    categoryOrProperty (element category or property)
        = "[" identificator "]" --inBrackets 
        | alnum+
        
    andOperation = andWord | "&&" | "&"

    andWord = ("A" | "a")("N" | "n")("D" | "d")
    
    orOperation = orWord | "||" | "|"

    orWord = ("O" | "o")("R" | "r")
    
    anyProperty = "*"
    
    identificator = (~"]" ~"[" any)+
    
    expressionConst = textConst | number
    
    textConst (target text value)
        = "\"" textValue "\""
        
    textValue = textChar*
    
    textChar
        = "\\\""
        | (~"\"" any)
        
    number
      = positiveNumber
      | negativeNumber
      
    negativeNumber = "-" positiveNumber
    
    positiveNumber = realNumber | integerNumber
      
    integerNumber = digit+
    
    realNumber = digit* "." digit+
}